// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // zx, nx
    // keeping x or false depending on if zx is enabled or not
    Mux16(a=x, b[0...15]=false, sel= zx, out= zdx);
    Not16(in=zdx, out= notx);
    Mux16(a=zdx, b= notx, sel= nx, out= ndx);
    
    // zy, ny
    Mux16(a=y, b[0..15]=false, sel=zy, out= zdy);
    Not16(in= zdy, out= noty);
    Mux16(a= zdy, b= noty, sel= ny, out= ndy);

    // f
    Add16(a= ndx, b= ndy, out= XaddY);
    And16(a= ndx, b= ndy, out= XandY);
    Mux16(a=XandY , b=XaddY , sel=f , out= xyd);

    // no
    Not16(in= xyd, out= notxyd);
    Mux16(a= xyd, b= notxyd, sel= no, out= out, out[15] = lastbit, out[0..7] = out1, out[8..15] = out2);

    // zr
    Or8Way(in= out1, out=zr1);
    Or8Way(in= out2, out= zr2);
    Not(in= zr1, out= notzr1);
    Not(in= zr2, out= notzr2);
    And(a= notzr1, b= notzr2, out= zr);

    // since the value is in 2's complement, last bit being a 1 makes it negative
    And(a=lastbit, b=true, out=ng);
}